apply plugin: 'com.android.library'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 22
        versionCode = 28
        versionName = '1.6.5-SNAPSHOT'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

ext {
    compileSdkVersion = 22
    buildToolsVersion = "22.2.0"

    minSdkVersion = 14
    targetSdkVersion = 22
}

dependencies {
    compile 'com.braintreepayments:card-form:2.0.1'
    compile project(':BraintreeApi')
    compile 'com.google.android.gms:play-services-wallet:[7.5.0,8.0.0)'

    androidTestCompile project(':TestUtils')
    compile(project(':TPFrameworkViews')) {
        transitive = false
    }
    compile project(':TPFrameworkTools')
    compile project(':TPFrameworkUtils')
}

task grantAnimationPermission(type: Exec, dependsOn: 'installDebugAndroidTest') {
    commandLine 'adb shell pm grant com.braintreepayments.api.dropin.test android.permission.SET_ANIMATION_SCALE'.split(' ')
}

tasks.whenTaskAdded { task ->
    if (task.name.startsWith('connectedAndroidTest')) {
        task.dependsOn grantAnimationPermission
    }
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

